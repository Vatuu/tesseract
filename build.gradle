import net.fabricmc.loom.task.RemapJarTask

plugins {
	id 'fabric-loom' version '0.2.5-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()
	maven { url "https://jitpack.io/"}
}
minecraft {}

dependencies {
	minecraft			"com.mojang:minecraft:${project.minecraft_version}"
	mappings			"net.fabricmc:yarn:${project.yarn_mappings}"
	modImplementation	"net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation 	"net.fabricmc.fabric-api:fabric-events-lifecycle-v0:${project.cmd_version}"
	modImplementation 	"net.fabricmc.fabric-api:fabric-api-base:${project.base_version}"
}

processResources {
	inputs.property "version", version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) { options.encoding = "UTF-8" }
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier "sources"
	from sourceSets.main.allSource
}
task apiJar(type: Jar, dependsOn: classes){
	classifier "api-dev"
	from(sourceSets.main.output){
		include "dev/vatuu/tesseract/api/**"
	}
}
task remapApiJar(type: RemapJarTask){
	dependsOn apiJar
	input = apiJar.outputs.files.singleFile
	addNestedDependencies = false
	classifier "api"
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
			artifact(remapApiJar){
				builtBy remapApiJar
			}
		}
	}

	repositories { mavenLocal() }
}